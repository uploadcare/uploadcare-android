// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    def versions = [:]
    versions.kotlin_version = '1.6.10'
    versions.kotlin_coroutines = '1.6.0'
    versions.min_sdk = 22
    versions.target_sdk = 30
    versions.compile_sdk = 30
    versions.jdk = JavaVersion.VERSION_1_8

    versions.gradle_version = '7.1.0'

    versions.core = "1.3.2"
    versions.material = "1.3.0-alpha03"
    versions.navigation = "2.3.1"
    versions.lifecycle = "2.2.0"
    versions.constraintlayout = "2.0.2"
    versions.annotation = '1.1.0'
    versions.appcompat = "1.2.0"
    versions.fragment = "1.6.0"
    versions.work = "2.4.0"

    versions.okhttp = "4.9.0"
    versions.retrofit = "2.9.0"

    versions.picasso = "2.71828"

    versions.moshi = "1.13.0"

    versions.junit = "4.12"
    versions.test_runner = "1.3.0"
    versions.espresso_core = "3.3.0"

    versions.dokka = "1.4.10.2"

    ext.versions = versions

    repositories {
        maven { url "https://maven.google.com" }
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$versions.gradle_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$versions.navigation"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$versions.dokka"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id 'base'
    id 'signing'
    id 'maven-publish'
}

allprojects {
    repositories {
        mavenLocal() // look into local .m2 cache.
        google()
        mavenCentral()
    }
}

subprojects {

    // apply publishing plugins to all subprojects to global control of publishing repositories.
    apply plugin: "base"
    apply plugin: "signing"
    apply plugin: "maven-publish"

    def isReleaseVersion = !version.toString().toLowerCase().endsWith("snapshot")

    tasks {
        // Always run tests as part of the `build` task.
        if(findByName("test")) {
            named("check") {
                dependOn(named("test"))
            }
        }
    }

    // Setup global publishing repository settings.
    signing {
        useGpgCmd()
        sign(publishing.publications)
    }

    publishing {
        repositories {
            maven {
                // Dynamically select either Maven Central or na Internal repository depending on the value of uploadcare.publish.type / UPLOADCARE_PUBLISH_TYPE
                name = "selected"

                // Allow deploying to a custom repository (for testing purposes)
                def publishInternally = project.findProperty("uploadcare.publish.type")?.toString() == "internal"
                def repositoryUrl

                if (isReleaseVersion) {
                    def releaseMavenCentral = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                    def releaseInternal = (project.findProperty("uploadcare.publish.internal.release") ?: System.getenv("UPLOADCARE_PUBLISH_INTERNAL_RELEASE") ?: "") as String
                    repositoryUrl = (releaseInternal != "" && publishInternally) ? releaseInternal: releaseMavenCentral
                } else {
                    def snapshotMavenCentral = "https://oss.sonatype.org/content/repositories/snapshots/"
                    def snapshotInternal = (project.findProperty("uploadcare.publish.internal.snapshot") ?: System.getenv("UPLOADCARE_PUBLISH_INTERNAL_SNAPSHOT") ?: "") as String
                    repositoryUrl = (snapshotInternal != "" && publishInternally) ? snapshotInternal : snapshotMavenCentral
                }

                url = uri(repositoryUrl)

                credentials {
                    def mavenCentralUser = (project.findProperty("uploadcare.publish.sonatype.user") ?: System.getenv("UPLOADCARE_PUBLISH_SONATYPE_USER") ?: "") as String
                    def mavenCentralPass = (project.findProperty("uploadcare.publish.sonatype.pass") ?: System.getenv("UPLOADCARE_PUBLISH_SONATYPE_PASS") ?: "") as String
                    def internalUser = (project.findProperty("uploadcare.publish.internal.user") ?: System.getenv("UPLOADCARE_PUBLISH_INTERNAL_USER") ?: "") as String
                    def internalPass = (project.findProperty("uploadcare.publish.internal.pass") ?: System.getenv("UPLOADCARE_PUBLISH_INTERNAL_PASS") ?: "") as String
                    username = (publishInternally) ? internalUser : mavenCentralUser
                    password = (publishInternally) ? internalPass : mavenCentralPass
                }
            }
        }
    }
}
